project.ext.set("archivesBaseName", "IndicKeyboard");

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
    }
}

apply plugin: 'com.android.application'

dependencies {
    implementation 'androidx.core:core:1.3.1'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation project(':ime')
}

// Used in calculating version code for each APK in splits
ext.versionCodeOffsets = ['arm64-v8a': 1, 'armeabi-v7a': 0, 'x86': 2, 'x86_64': 3]

import com.android.build.OutputFile

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "org.smc.inputmethod.indic"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 75
        versionName "3.4"
    }

    splits {
      abi {
        enable true
        universalApk false
        reset()
        include "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
      }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['../java-overridable/src', 'src', '../common/src', '../inputmethodcommon/java']
            jniLibs.srcDirs = ['jniLibs']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        androidTest.setRoot('tests')
    }

    aaptOptions {
        noCompress ".dict"
    }

    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("INDIC_KEYSTORE")))
            storePassword System.getenv("INDIC_KEYSTORE_PASSWORD")
            keyAlias System.getenv("INDIC_KEY_ALIAS")
            keyPassword System.getenv("INDIC_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    android.applicationVariants.all { variant ->
      variant.outputs.each { output ->
        // Uses a hardcoded lookup instead of loop index because in F-droid build
        // we overwrite splits and make only one output at a time.
        output.versionCodeOverride =
          variant.versionCode + project.ext.versionCodeOffsets.get(output.getFilter(OutputFile.ABI))
      }
    }
}
